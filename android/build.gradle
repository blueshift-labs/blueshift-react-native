buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            jcenter()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:7.3.1'
        }
    }
}

apply plugin: 'com.android.library'

ext.sdkVersion = '3.5.1'
ext.pluginVersion = '1.2.3'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('BlueshiftReactNative_compileSdkVersion', 33)
    defaultConfig {
        minSdkVersion safeExtGet('BlueshiftReactNative_minSdkVersion', 21)
        targetSdkVersion safeExtGet('BlueshiftReactNative_targetSdkVersion', 33)
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'SDK_VERSION', "\"$sdkVersion\""
        buildConfigField 'String', 'PLUGIN_VERSION', "\"$pluginVersion\""
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation "com.blueshift:android-sdk-x:$sdkVersion"  // From node_modules
}
